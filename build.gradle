plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.7.0'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'dev.syrent'
archivesBaseName = project.name
version = findProperty("version")
description = 'Wanted'

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        name = "papermc"
        url = uri('https://repo.papermc.io/repository/maven-public/')
    }

    maven {
        name = "ruom"
        url = uri("https://maven.pkg.github.com/mohamad82bz/ruom")
        credentials {
            username = project.findProperty("gpr.user")
            password = project.findProperty("gpr.key")
        }
    }
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.7.10'
    //implementation 'org.bstats:bstats-bukkit:2.2.1'
    implementation 'me.mohamad82:ruom-bukkit:4.9.2'
    compileOnly 'io.papermc.paper:paper-api:1.19.2-R0.1-SNAPSHOT'
    //compileOnly 'me.clip:placeholderapi:2.10.10'
    //compileOnly 'net.citizensnpcs:citizens-main:2.0.28-SNAPSHOT'
    //compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.5'
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

compileJava {
    options.encoding = "UTF-8"
}

compileKotlin {
    kotlinOptions.javaParameters = true
}

jar {
    archiveFileName = findProperty("plugin-name") + " v" + findProperty("version") + " " + "unshaded" + ".jar"
}

shadowJar {
    archiveFileName = findProperty("plugin-name") + " v" + findProperty("version") + ".jar"
    relocate('me.mohamad82.ruom', 'ir.syrent.wanted.ruom')
    dependencies {
        exclude(dependency('net.kyori:.*:.*'))
        exclude(dependency('com.zaxxer:HikariCP:.*'))
        exclude(dependency('com.github.cryptomorin:XSeries:.*'))
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Jar) {
    destinationDirectory = file("$rootDir/bin/")
}

clean.doLast {
    file("${rootDir}/bin").deleteDir()
}

build.dependsOn shadowJar